# A personalized version of 
# Jeff Sandberg's paradox theme (http://www.paradox.io/posts/9-my-new-zsh-prompt)
# with bits of 
# DigitalFormula's modified agnoster theme for Prezto/YADR (https://github.com/digitalformula/zsh.prompts)
# and David J. Rice's Powerline for Prezto (https://github.com/davidjrice/prezto_powerline)
# and some rearranging and color changing and whatnot :)


# Load dependencies.
pmodload 'helper'

# Define variables.
_prompt_skittleys_current_bg='NONE'
_prompt_skittleys_segment_separator=''
_prompt_skittleys_start_time=$SECONDS

function prompt_skittleys_start_segment {
  local bg fg
  [[ -n "$1" ]] && bg="%K{$1}" || bg="%k"
  [[ -n "$2" ]] && fg="%F{$2}" || fg="%f"
  if [[ "$_prompt_skittleys_current_bg" != 'NONE' && "$1" != "$_prompt_skittleys_current_bg" ]]; then
    print -n " $bg%F{$_prompt_skittleys_current_bg}$_prompt_skittleys_segment_separator$fg "
  else
    print -n "$bg$fg "
  fi
  _prompt_skittleys_current_bg="$1"
  [[ -n "$3" ]] && print -n "$3"
}

function prompt_skittleys_end_segment {
  if [[ -n "$_prompt_skittleys_current_bg" ]]; then
    print -n " %k%F{$_prompt_skittleys_current_bg}$_prompt_skittleys_segment_separator"
  else
    print -n "%k"
  fi
  print -n "%f"
  _prompt_skittleys_current_bg=''
}

function prompt_skittleys_build_prompt {
  prompt_skittleys_start_segment magenta white '$_prompt_skittleys_pwd'
  #prompt_skittleys_start_segment magenta white "$(get_short_path)"

  if [[ -n "$git_info" ]]; then
    prompt_skittleys_start_segment blue white '${(e)git_info[ref]}${(e)git_info[status]}'
  else
    prompt_skittleys_start_segment blue white
  fi

  prompt_skittleys_start_segment black default '%(?::%F{red}✘ )%(!:%F{yellow}⚡ :)%(1j:%F{cyan}⚙ :)'

  prompt_skittleys_end_segment
}

function prompt_skittleys_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_skittleys_pwd="$MATCH"
    unset MATCH
  else
    _prompt_skittleys_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

#function prompt_skittleys_print_elapsed_time {
#  local end_time=$(( SECONDS - _prompt_skittleys_start_time ))
#  local hours minutes seconds remainder
#
#  if (( end_time >= 3600 )); then
#    hours=$(( end_time / 3600 ))
#    remainder=$(( end_time % 3600 ))
#    minutes=$(( remainder / 60 ))
#    seconds=$(( remainder % 60 ))
#    print -P "%B%F{red}>>> elapsed time ${hours}h${minutes}m${seconds}s%b"
#  elif (( end_time >= 60 )); then
#    minutes=$(( end_time / 60 ))
#    seconds=$(( end_time % 60 ))
#    print -P "%B%F{yellow}>>> elapsed time ${minutes}m${seconds}s%b"
#  elif (( end_time > 10 )); then
#    print -P "%B%F{green}>>> elapsed time ${end_time}s%b"
#  fi
#}

function prompt_skittleys_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_skittleys_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Calculate and print the elapsed time.
  #prompt_skittleys_print_elapsed_time
}

function prompt_skittleys_preexec {
  _prompt_skittleys_start_time="$SECONDS"
}

function prompt_skittleys_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook preexec prompt_skittleys_preexec
  add-zsh-hook precmd prompt_skittleys_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%B%F{blue}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '%F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%B%F{red}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ' %{%F{yellow}%}! %s%{%f%}'
  zstyle ':prezto:module:git:info:added' format ' %{%F{green}%}✚%{%f%}'
  zstyle ':prezto:module:git:info:ahead' format ' %{%F{yellow}%}⬆%{%f%}'
  zstyle ':prezto:module:git:info:behind' format ' %{%F{yellow}%}⬇%{%f%}'
  zstyle ':prezto:module:git:info:branch' format '⚑  %b'
  zstyle ':prezto:module:git:info:commit' format ':%{%F{green}%}➦ %.7c%{%f%}'
  zstyle ':prezto:module:git:info:deleted' format ' %{%F{red}%}✖%{%f%}'
  zstyle ':prezto:module:git:info:dirty' format ' ⁝'
  zstyle ':prezto:module:git:info:modified' format ' %{%F{black}%}✱%{%f%}'
  zstyle ':prezto:module:git:info:position' format ':%{%F{green}%}%p%{%f%}'
  zstyle ':prezto:module:git:info:renamed' format ' %{%F{magenta}%}➜%{%f%}'
  zstyle ':prezto:module:git:info:stashed' format ' %{%F{cyan}%}✭%{%f%}'
  zstyle ':prezto:module:git:info:unmerged' format ' %{%F{yellow}%}═%{%f%}'
  zstyle ':prezto:module:git:info:untracked' format ' %{%F{white}%}?%{%f%}'
  zstyle ':prezto:module:git:info:keys' format \
    'ref' '$(coalesce "%b" "%p" "%c")' \
    'status' '%C%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='${(e)$(prompt_skittleys_build_prompt)} '
  RPROMPT='%t'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_skittleys_setup "$@"
